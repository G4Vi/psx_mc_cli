#!/usr/bin/perl
use strict; use warnings;
use feature 'say';
use Getopt::Long qw(GetOptions);
Getopt::Long::Configure qw(gnu_getopt); 
use FindBin;
use lib "$FindBin::Bin/../lib";
use PSX::MC;
#use Devel::Peek;

sub print_directory {
	my ($dir) = @_;	 
    
	my %blockstring = (		
		0x52 => 'MIDLINK',
		0x53 => 'ENDLINK',
		0xA0 => 'EMPTY',
		0xA1 => 'EMPTY',
		0xA2 => 'EMPTY',
		0xA3 => 'EMPTY',
		0xFF => 'UNUSABLE'
	);
	my $inusestr = $blockstring{$dir->{'inuse'}};
	if(! $inusestr) {
		if($dir->{'inuse'}== 0x51) {
			$inusestr = ($dir->{'linkindex'} == 0xFFFF) ? 'INUSE' : 'SRTLINK';			
		}		
		else {
			$inusestr = sprintf("0x%X", $dir->{'inuse'});
		}
	}
	my $linkstr = sprintf("0x%04X", $dir->{'linkindex'});	
	my $datastring = ((($dir->{'datasize'} % 0x2000) == 0) && ($dir->{'calcblocks'} <= 15)) ? sprintf("%2u blocks", $dir->{calcblocks}) : $dir->{datasize}. " bytes";
    my $xorstring = ($dir->{'calcxor'} == $dir->{'xor'}) ? sprintf("xor 0x%X", $dir->{'xor'}) : sprintf("xor bad, calculated 0x%X stored 0x%x", $dir->{'calcxor'}, $dir->{'xor'});

    say sprintf("%-8s %-20s %s %-20s %s", $inusestr, $dir->{'codename'}, $linkstr, $datastring, $xorstring);	
}

sub handleFile {
	my ($contents, $dataoffset) = @_;
	my $filebuf = substr($contents, $dataoffset, 0x80);
	my $fileheader = PSX::MC::parse_file_header($filebuf);
	say $fileheader->{'title'};
	
	# get the clut colors, not currently used
	my $red_mask = 0x1F;
    my $green_mask = 0x3E0;
    my $blue_mask = 0x7C00;
	foreach my $rgb555 (@{$fileheader->{'clut'}}) {
		my $red = ($rgb555 & $red_mask) << 3;
		my $green = (($rgb555 & $green_mask) >> 5) << 3;
		my $blue = (($rgb555 & $blue_mask) >> 10) << 3;
	}

    # print the icon
	my $foffset = $dataoffset+0x80;
	for(my $i = 0; $i < $fileheader->{'framecnt'}; $i++) {
		my $framebuf = substr($contents, $foffset, 0x80);		
		for(my $y = 0; $y < 16; $y++) {
			for(my $x = 0; $x < 16; $x++) {
				my $ch = vec($framebuf, ($y * 16)+$x, 4);
				print(($ch) ? "X" : " ");
				print(($ch) ? "X" : " ");
			}
			print("\n");
		}
		$foffset += 0x80;
	}
}

binmode(STDOUT, "encoding(UTF-8)");
#GetOptions(
#    'data|d' => \$data,
#) or die "Usage: $0 --data\n";

# print the saves
foreach my $file (@ARGV) {
	say "$file:";
	my $mcfile = PSX::MC->load($file);
	if(! $mcfile) {
		warn("unable to load: $file");
		next;
	}
	if($mcfile->{'hasdir'}) {
		my $filestart;
		$mcfile->foreachDirEntry(sub {
			my ($entry, $dataoffset, $entrydata, $i) = @_;
			print_directory($entry);
			# if the entry has data assocated, save the offset
			if($entry->{'calcblocks'}) {
				$filestart = $dataoffset;
			}
			# if entry is part of mcs or is a single block save or is and endlink read the file
			if(($mcfile->{'type'} eq 'mcs') ||
			((($entry->{'inuse'}== 0x51) && ($entry->{'linkindex'} == 0xFFFF)) || ($entry->{'inuse'} == 0x53))) {
				handleFile($mcfile->{'contents'}, $filestart);
				$filestart = '';
			}
		});
	}
	else {
		warn("skipping unhandled format");
	}

	print "\n";
}
